buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        // publish release artifacts to github
        classpath 'com.novoda:bintray-release:0.3.4'
        // create safely a new release tag
        classpath 'pl.allegro.tech.build:axion-release-plugin:1.3.4'
        // publish release artifacts to github
        classpath "co.riiid:gradle-github-plugin:0.4.2"
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'

apply from: "$project.rootDir/gradle/projectLocalRepo.gradle"
apply from: "$project.rootDir/gradle/jacocoCoverage.gradle"
apply from: "$project.rootDir/gradle/release.gradle"

version currentReleaseVersion
group = "de.nenick"
def androidPluginVersion = "2.3.0"

project.sourceCompatibility = JavaVersion.VERSION_1_7
project.targetCompatibility = JavaVersion.VERSION_1_7

dependencies {
    compile library.AndroidAnnotations
    compile project(':mvp-plugin-api')

    testCompile "com.android.tools.build:gradle:$androidPluginVersion"
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4', {
        exclude group: "org.codehaus.groovy", module: 'groovy-all'
    }
    testCompile files("../build/generated/mockable-android-25.default-values.jar")
    testCompile gradleTestKit()
    testCompile project(":mvp-plugin-sample")
}

test {
    maxHeapSize = "2048m"
    jvmArgs "-XX:MaxPermSize=256m"
}

publish {
    artifactId = 'androidannotations-mvp'
}

tasks.install.repositories.mavenDeployer.pom.artifactId = 'androidannotations-mvp'

tasks.withType(JavaCompile) {
    if (name.toLowerCase().contains("test")) {
        dependsOn install
        dependsOn(":mvp-plugin-api:install")
        dependsOn(":mvp-plugin-sample:mockableAndroidJar")
    }
}